apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

description = "Gradle Git Version Plugin"

println "$group:${project.name}:$version"

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile gradleApi()
    compile localGroovy()

    testCompile 'se.lovef:kotlin-assert-utils:0.5.0'
}

//noinspection GroovyAssignabilityCheck,GrUnresolvedAccess
task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

def mavenDir = "build/repo"

def info = [
        "github": "https://github.com/lovef/gradle-git-version",
        "git"   : "https://github.com/lovef/gradle-git-version.git",
        "issues": "https://github.com/lovef/gradle-git-version/issues",
]

//noinspection GrUnresolvedAccess
publishing {
    publications {
        maven(MavenPublication) {
            artifact sourceJar
            from components.java
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.name
                    description description
                    url info.github
                    scm {
                        url info.github
                        connection "scm:git:${info.git}"
                        developerConnection "scm:git:${info.git}"
                    }
                    licenses {
                        license {
                            name 'Apache License Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'lovef'
                            name 'Love F'
                            email 'lovef.code@gmail.com'
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url mavenDir
        }
    }
}

//noinspection GrUnresolvedAccess
bintray {
    user = project.properties.BINTRAY_API_USER ?: System.getenv('BINTRAY_API_USER')
    key = project.properties.BINTRAY_API_KEY ?: System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    //noinspection GrUnresolvedAccess
    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description
        userOrg = user
        licenses = ['Apache-2.0']
        labels = ["gradle", "gradle-plugin"]
        websiteUrl = info.github
        vcsUrl = info.git
        //noinspection GroovyAssignabilityCheck
        publish = false
        publicDownloadNumbers = true

        //noinspection GroovyAssignabilityCheck
        version {
            name = project.version
            vcsTag = gitVersion.tag
            desc = project.description
            //noinspection GrUnresolvedAccess
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = null //Optional. The passphrase for GPG signing'
            }
        }
    }
}
